HAI 1.3
	CAN HAS STRING?
	CAN HAS STDIO?

	I HAS A LIZT ITZ ""
	I HAS A LETTAHS ITZ I IZ STDIO'Z OPEN YR "DOODZ" AN YR "r" MKAY

	I IZ STDIO'Z DIAF YR LETTAHS MKAY, O RLY?
    YA RLY
    NO WAI
    OIC

	I HAS A BIGNESS ITZ 1024
	IM IN YR HOWSH UPPIN YR CHIMNEY TIL DIFFRINT 1024 AN BIGNESS
		I HAS A NAMEZ ITZ I IZ STDIO'Z LUK YR LETTAHS AN YR 1024 MKAY
	    BIGNESS R I IZ STRING'Z LEN YR NAMEZ MKAY
	    LIZT R SMOOSH LIZT AN NAMEZ MKAY
	IM OUTTA YR HOWSH

	LIZT R SMOOSH LIZT AN "" MKAY

	I IZ STDIO'Z CLOSE YR LETTAHS MKAY

	BTW VISIBLE LIZT

	HOW IZ I NICE YR NAME
		BIGNESS R I IZ STRING'Z LEN YR NAME MKAY
		IM IN YR STUFF UPPIN YR EYE TIL BOTH SAEM EYE AN DIFF OF BIGNESS AN 1
			I HAS A LETTAH ITZ I IZ STRING'Z AT YR NAME AN YR EYE MKAY
			I HAS A LETTAH2 ITZ I IZ STRING'Z AT YR NAME AN YR SUM OF 2 AN EYE MKAY
			BOTH SAEM LETTAH AN LETTAH2
			O RLY?
				YA RLY, FOUND YR WIN
				NO WAI
			OIC
		IM OUTTA YR STUFF
		FOUND YR FAIL
	IF U SAY SO

	HOW IZ I NICER YR NAME
		BIGNESS R I IZ STRING'Z LEN YR NAME MKAY
		IM IN YR FIRST UPPIN YR EYE TIL BOTH SAEM EYE AN BIGNESS
			IM IN YR SECOND UPPIN YR JAY TIL BOTH SAEM JAY AN DIFF OF BIGNESS AN 1
				I HAS A LOWER ITZ SUM OF 1 AN EYE 
				DIFFRINT LOWER AN BIGGR OF LOWER AN JAY
				O RLY?
					YA RLY
						I HAS A LETTAH1 ITZ I IZ STRING'Z AT YR NAME AN YR EYE MKAY
						I HAS A LETTAH2 ITZ I IZ STRING'Z AT YR NAME AN YR SUM OF 1 AN EYE MKAY
						I HAS A KUPLE ITZ SMOOSH LETTAH1 AN LETTAH2 MKAY
						I HAS A LETTAH3 ITZ I IZ STRING'Z AT YR NAME AN YR JAY MKAY
						I HAS A LETTAH4 ITZ I IZ STRING'Z AT YR NAME AN YR SUM OF 1 AN JAY MKAY
						I HAS A KUPLE2 ITZ SMOOSH LETTAH3 AN LETTAH4 MKAY
						BOTH SAEM KUPLE AN KUPLE2
						O RLY?
							YA RLY, FOUND YR WIN
							NO WAI
						OIC
					NO WAI
				OIC
			IM OUTTA YR SECOND
		IM OUTTA YR FIRST
		FOUND YR FAIL
	IF U SAY SO

	HOW IZ I NAWTY YR NAME
		FOUND YR NOT ALL OF I IZ NICE YR NAME MKAY AN I IZ NICER YR NAME MKAY MKAY
	IF U SAY SO

	I HAS A NAME ITZ ""
	I HAS A NICEONES ITZ 0
	IM IN YR OFISH UPPIN YR EYE TIL BOTH SAEM EYE AN I IZ STRING'Z LEN YR LIZT MKAY
		I HAS A LETTAH ITZ I IZ STRING'Z AT YR LIZT AN YR EYE MKAY
		DIFFRINT LETTAH AN ":)"
		O RLY?
			YA RLY, NAME R SMOOSH NAME AN LETTAH MKAY
			MEBBE I IZ NAWTY YR NAME MKAY
				VISIBLE NAME ": NAWTY!"
				NAME R ""
			NO WAI
				VISIBLE NAME ": NICE!"
				NICEONES R SUM OF 1 AN NICEONES
				NAME R ""
		OIC
	IM OUTTA YR OFISH

	VISIBLE NICEONES
KTHXBYE

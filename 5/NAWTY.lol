HAI 1.3
	CAN HAS STRING?
	CAN HAS STDIO?

	I HAS A LIZT ITZ ""
	I HAS A LETTAHS ITZ I IZ STDIO'Z OPEN YR "DOODZ" AN YR "r" MKAY

	I IZ STDIO'Z DIAF YR LETTAHS MKAY, O RLY?
    YA RLY
    NO WAI
    OIC

	I HAS A BIGNESS ITZ 1024
	IM IN YR HOWSH UPPIN YR CHIMNEY TIL DIFFRINT 1024 AN BIGNESS
		I HAS A NAMEZ ITZ I IZ STDIO'Z LUK YR LETTAHS AN YR 1024 MKAY
	    BIGNESS R I IZ STRING'Z LEN YR NAMEZ MKAY
	    LIZT R SMOOSH LIZT AN NAMEZ MKAY
	IM OUTTA YR HOWSH

	LIZT R SMOOSH LIZT AN "" MKAY

	I IZ STDIO'Z CLOSE YR LETTAHS MKAY

	BTW VISIBLE LIZT

	HOW IZ I NICE YR NAME
		BIGNESS R I IZ STRING'Z LEN YR NAME MKAY
		I HAS A VOWLZ ITZ 0
		IM IN YR STUFF UPPIN YR EYE TIL BOTH SAEM EYE AN BIGNESS
			I HAS A LETTAH ITZ I IZ STRING'Z AT YR NAME AN YR EYE MKAY
			LETTAH, WTF?
			  OMG "a"
			  OMG "e"
			  OMG "i"
			  OMG "o"
			  OMG "u"
			  	VOWLZ R SUM OF 1 AN VOWLZ
			OIC
		IM OUTTA YR STUFF
		FOUND YR DIFFRINT VOWLZ AN SMALLR OF VOWLZ AN 2
	IF U SAY SO

	HOW IZ I NICER YR NAME
		BIGNESS R I IZ STRING'Z LEN YR NAME MKAY
		I HAS A MEMRY ITZ ""
		IM IN YR STUFF UPPIN YR EYE TIL BOTH SAEM EYE AN BIGNESS
			I HAS A LETTAH ITZ I IZ STRING'Z AT YR NAME AN YR EYE MKAY
			BOTH SAEM LETTAH AN MEMRY
			O RLY?
			YA RLY
				FOUND YR WIN
			OIC
			MEMRY R LETTAH
		IM OUTTA YR STUFF
		FOUND YR FAIL
	IF U SAY SO

	HOW IZ I NICEST YR NAME
		BIGNESS R I IZ STRING'Z LEN YR NAME MKAY
		I HAS A MEMRY ITZ ""
		IM IN YR STUFF UPPIN YR EYE TIL BOTH SAEM EYE AN BIGNESS
			I HAS A LETTAH ITZ I IZ STRING'Z AT YR NAME AN YR EYE MKAY
			I HAS A KUPLE ITZ SMOOSH MEMRY AN LETTAH MKAY
			KUPLE, WTF?
				OMG "ab"
				OMG "cd"
				OMG "pq"
				OMG "xy"
					FOUND YR FAIL
			OIC
			MEMRY R LETTAH
		IM OUTTA YR STUFF
		FOUND YR WIN
	IF U SAY SO

	HOW IZ I NAWTY YR NAME
		FOUND YR NOT ALL OF I IZ NICE YR NAME MKAY AN I IZ NICER YR NAME MKAY AN I IZ NICEST YR NAME MKAY MKAY
	IF U SAY SO

	I HAS A NAME ITZ ""
	I HAS A NICEONES ITZ 0
	IM IN YR OFISH UPPIN YR EYE TIL BOTH SAEM EYE AN I IZ STRING'Z LEN YR LIZT MKAY
		I HAS A LETTAH ITZ I IZ STRING'Z AT YR LIZT AN YR EYE MKAY
		DIFFRINT LETTAH AN ":)"
		O RLY?
			YA RLY, NAME R SMOOSH NAME AN LETTAH MKAY
			MEBBE I IZ NAWTY YR NAME MKAY
				VISIBLE NAME ": NAWTY!"
				NAME R ""
			NO WAI
				VISIBLE NAME ": NICE!"
				NICEONES R SUM OF 1 AN NICEONES
				NAME R ""
		OIC
	IM OUTTA YR OFISH

	VISIBLE NICEONES
KTHXBYE
